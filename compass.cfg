// gets the axis that the player is looking along, given the arguments spitch, syaw
// spitch is the pitch, from -1 to 1
// syaw is the yaw, from 0 to 3
// returns the axis information (axis name, axis number, axis direction)
getaxis = [
	// arg1 = spitch, arg2 = syaw

	// formats axis info given pos/neg axis number
	formai = [
		sign = (substr $arg1 0 1) // +/-
		anum = (substr $arg1 1 1) // axis number
		// [(+/-)(x/y/z) 0/1/2 1/-1]
		concat (concatword $sign (at [ z x y ] $anum)) $anum (? (strcmp $sign +) 1 -1)
	]
	// if pitch is closest to level (0), return a horizontal axis
	if $arg1 [
		// axis number 2 (y, vertical)
		formai (? (< $arg1 0) -2 +2)
	] [
		ifcount $arg2 [
			// North, axis 1 (x)
			formai +1 ] [
			// East, axis 0 (z)
			formai -0 ] [
			// South
			formai -1 ] [
			// West
			formai +0
		]
	] 
]

// GUI to show the heading and pitch, as well as other info (axis and such)

newgui compass [
	yaw = $getcamyaw
	pitch = $getcampitch
	
	// raw values
	guititle Raw Values
	guitext (concat Yaw: $yaw)
	guitext (concat Pitch: $pitch)

	// heading and pitch
	guititle Heading/Pitch
	// divide by 90, round, and modulate to get 0-3 (Simple YAW)
	syaw = (mod (round (divf $yaw 90)) 4)
	heading = (
		ifcount $syaw [
			result North ] [
			result East  ] [
			result South ] [
			result West
		]
	)
	guitext (concat Heading: $heading)
	// down if it is negative
	guitext (concat Pitch: (? (< $pitch 0) Down Up))

	// axis
	guititle Axis
	// put pitch to -1 to 1
	// divide by 90 and round (Simple PITCH)
	spitch = (round (divf $pitch 90))
	axis = (getaxis $spitch $syaw)
	guitext (concat Axis: (at $axis 0))
	guitext (concat Axis Number: (at $axis 1))
	guitext (concat Axis Direction: (at $axis 2))

	// editing actions
	if $editing [
		guititle Editing Actions
		// move selection by units in the direction the player is facing
		guilist [
			guitextbox [Move selection ] 12
			guifield units 4 [movesel (* $units (at $axis 2)) (at $axis 1)]
			guitextbox [ units] 5
		]
	]
]
